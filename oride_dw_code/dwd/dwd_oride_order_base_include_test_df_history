insert overwrite table oride_dw.dwd_oride_order_base_include_test_df partition(country_code='nal',dt='his')
select t1.id AS order_id, --订单号
 nvl(t1.city_id,-999) AS city_id, --城市
 t1.serv_type AS product_id, --订单车辆类型(0: 专快混合 1:driect[专车] 2: street[快车] 99:招手停)
 t1.user_id AS passenger_id, -- 乘客ID
 t1.start_name,
 t1.start_lng,
 t1.start_lat,
 t1.end_name,
 t1.end_lng,
 t1.end_lat,
 t1.duration,
 t1.distance,
 t1.basic_fare,
 t1.dst_fare,
 t1.dut_fare,
 t1.dut_price,
 t1.dst_price,
 t1.price,
 t1.reward,
 t1.driver_id,
 t1.plate_num,
 from_unixtime(t1.take_time,'yyyy-MM-dd hh:mm:ss') as take_time, --接单（应答）时间
from_unixtime(t1.wait_time,'yyyy-MM-dd hh:mm:ss') as wait_time, --到达接送点时间
from_unixtime(t1.pickup_time,'yyyy-MM-dd hh:mm:ss') as pickup_time, --接到乘客时间
from_unixtime(t1.arrive_time,'yyyy-MM-dd hh:mm:ss') as arrive_time, --到达终点时间
from_unixtime(t1.finish_time,'yyyy-MM-dd hh:mm:ss') as finish_time, --订单（支付）完成时间
t1.cancel_role, --取消人角色(1: 用户, 2: 司机, 3:系统 4:Admin)
from_unixtime(t1.cancel_time,'yyyy-MM-dd hh:mm:ss') as cancel_time, --取消时间
t1.cancel_type, --取消原因类型
t1.cancel_reason,
t1.status, --订单状态 (0: wait assign, 1: pick up passenger, 2: wait passenger, 3: send passenger, 4: arrive destination, 5: finished, 6: cancel)
from_unixtime(t1.create_time,'yyyy-MM-dd hh:mm:ss') as create_time, --创建时间
t1.fraud, ----是否欺诈(0否1是)
t1.driver_serv_type, -- 司机服务类型(1: Direct 2:Street )
t1.refund_before_pay,
t1.refund_after_pay,
t1.abnormal, --异常状态(0 否 1 逃单)
t1.flag_down_phone,
t1.zone_hash, --所属区域
 t1.updated_at AS updated_time,
 from_unixtime(t1.create_time,'yyyy-MM-dd') AS create_date,
 if(t1.driver_id <> 0,1,0) AS is_request, --是否接单（应答）
if(t1.status IN(4,5),1,0) AS is_finish, --是否完单
if(t1.pickup_time <> 0,t1.pickup_time - t1.take_time,0) AS pick_up_dur, --接驾时长（秒）
if(t1.take_time <> 0,t1.take_time - t1.create_time,0) AS take_dur, --应答时长（秒）
if(t1.cancel_time>0
   AND t1.take_time>0,t1.cancel_time - t1.take_time,0) AS cannel_pick_dur, --取消接驾时长（秒）
if(t1.pickup_time>0
   AND t1.wait_time>0,t1.pickup_time - t1.wait_time,0) AS wait_dur, --等待上车时长（秒）
if(t1.arrive_time>0
   AND t1.take_time > 0,t1.arrive_time - t1.take_time,0) AS service_dur, --服务时长（秒）
if(t1.arrive_time>0
   AND t1.pickup_time > 0,t1.arrive_time - t1.pickup_time,0) AS billing_dur, --计费时长（秒）
if(t1.status = 5
   AND t1.finish_time>0
   AND t1.arrive_time > 0,t1.finish_time - t1.arrive_time,0) AS pay_dur, -- 支付时长(秒)
if(t1.status = 6
   AND t1.cancel_role IN(3,4),1,0) AS is_sys_cancel, -- 是否系统取消
if(t1.status = 6
   AND t1.driver_id = 0
   AND t1.cancel_role = 1,1,0) AS is_passanger_before_cancel, --是否乘客应答前取消
if(t1.status = 6
   AND t1.driver_id <> 0
   AND t1.cancel_role = 1,1,0) AS is_passanger_after_cancel, --是否乘客应答后取消
if(t1.status = 6
   AND t1.driver_id <> 0
   AND t1.cancel_role = 2,1,0) AS is_driver_after_cancel, --是否司机应答后取消
if(t1.status=5,1,0) AS is_finish_pay, --是否完成支付
nvl(t2.amount,0) AS pay_amount, --实际支付金额
nvl(t2.mode,0) AS pay_mode, --支付方式（0: 未知, 1: 线下支付, 2: opay, 3: 余额）
nvl(t2.coupon_id,0) AS coupon_id, --优惠券ID
t2.coupon_name, -- 优惠券名称
t2.coupon_amount, --优惠券金额
t2.bonus, --使用奖励金
t2.balance, --使用余额
t2.opay_amount, --opay支付金额
t2.reference, -- opay流水号
t2.currency, -- opay货币类型
t2.status AS pay_status, -- 订单支付状态
t2.pay_type, -- 订单支付类型
t2.tip, -- 小费
if(t1.status IN(4,5)
   AND t1.arrive_time>0
   AND t1.pickup_time > 0,t1.arrive_time - t1.pickup_time,0) AS finish_billing_dur, --完单计费时长（秒）
if(t1.driver_id <> 0
   AND t1.take_time > 0
   AND t1.finish_time>0,t1.finish_time - t1.take_time,0) AS finish_order_dur -- 完成做单时长（秒）
from (select * FROM oride_dw.ods_sqoop_base_data_order_expired_df
WHERE dt='2019-09-01') t1 left outer join
(select * from oride_dw.ods_sqoop_base_data_order_payment_df
WHERE dt='2019-09-01') t2
on t1.id=t2.id
left outer join
(select * from oride_dw.dwd_oride_order_base_include_test_df
where dt='2019-09-01') t3 on t1.id=t3.order_id
where t3.order_id is null;